default:
    just --list
    
deploy_and_init: deploy init_social_graph
    
deploy:
    docker stack deploy --compose-file=docker-compose-mimetic.yml social_network
    
undeploy:
    docker stack rm social_network
    
init_social_graph:
    python3 scripts/init_social_graph.py --graph=socfb-Reed98

get_jaeger_services:
    curl -X 'GET' \
        'http://0.0.0.0:16686/api/services' \
        -H 'accept: application/json'

get_jaeger_traces service lookback limit:
    curl -X 'GET' \
        'http://0.0.0.0:16686/api/traces?service={{service}}&lookback={{lookback}}&limit={{limit}}' \
        -H 'accept: application/json'

_compose rps duration='60':
    ../wrk2/wrk -D exp -t 10 -c 10 -d {{duration}} -L -s \
        ./wrk2/scripts/social-network/compose-post.lua \
        http://0.0.0.0:8080/wrk2-api/post/compose \
        -R {{rps}}

compose rps stats trace duration='60' limit='1000' :
    @just _compose {{rps}} {{duration}} > {{stats}}
    @just get_jaeger_traces compose-post-service {{duration}}000 {{limit}} > {{trace}}
